#compdef inkscape

typeset -a args=(
  '(- *)'--help'[Show this help message]'
  '(- *)'{-V,--version}'[Print the Inkscape version number]'
  '(- *)'--system-data-directory'[Prints the system data directory where data files that ship with Inkscape are stored.]'
  '(- *)'--user-data-directory'[Prints the user profile directory where user-specific data files and preferences are stored.]'
  '(-p --pipe)'{-p,--pipe}'[Reads input file from standard input (stdin).]'
  --pdf-page='[Imports the given page of a pdf file. Numbering starts with 1.]:PAGE'
  --pdf-poppler
  --convert-dpi-method='[Choose method used to rescale legacy (pre-0.92) files which render slightly smaller due to the switch from 90 DPI to 96 DPI when interpreting lengths expressed in units of pixels.]:METHOD:(none scale-viewbox scale-document)'
  --no-convert-text-baseline-spacing'[Do not automatically fix text baselines in legacy (pre-0.92) files on opening.]'
  '(-o --export-filename)'{-o,--export-filename=}'[Sets the name of the output file.]:FILENAME:_files'
  --export-overwrite'[Overwrites input file.]'
  --export-type='[Specify the file type to export.]:TYPE:_values -s, type svg png ps eps pdf emf wmf xaml'
  '(-z --without-gui)'{-z,--without-gui}'[Do not use X server (only process files from console)]'
  '(-C --export-area-page)'{-C,--export-area-page}'[In SVG, PNG, PDF, PS exported area is the page.]'
  '(-D --export-area-drawing)'{-D,--export-area-drawing}'[In SVG, PNG, PDF, PS, and EPS export, exported area is the drawing (not page), i.e. the bounding box of all objects of the document (or of the exported object if --export-id is used).]'
  '(-a --export-area)'{-a,--export-area=}'[In PNG export, set the exported area in SVG user units (anonymous length units normally used in Inkscape SVG).]:x0\:y0\:x1\:y1'
  --export-area-snap'[For PNG export, snap the export area outwards to the nearest integer SVG user unit (px) values.]'
  '(-d --export-dpi)'{-d,--export-dpi=}'[The resolution used for PNG export.]:DPI'
  '(-w --export-width)'{-w,--export-width=}'[The width of generated bitmap in pixels.]:WIDTH'
  '(-h --export-height)'{-h,--export-height=}'[The height of generated bitmap in pixels.]:HEIGHT'
  --export-margin='[Adds a margin around the exported area.]:MARGIN'
  '(-i --export-id)'{-i,--export-id=}'[For PNG, PS, EPS, PDF and plain SVG export, the id attribute value of the object(s) that you want to export from the document; all other objects are not exported.]:ID'
  '(-j --export-id-only)'{-j,--export-id-only}'[For PNG and plain SVG, only export the object whose id is given in --export-id.]'
  '(-l --export-plain-svg)'{-l,--export-plain-svg}'[Export document(s) to plain SVG format, without sodipodi: or inkscape: namespaces and without RDF metadata.]'
  --export-ps-level='[Set language version for PS and EPS export.]:LEVEL:(2 3)'
  --export-pdf-version='[Select the PDF version of the exported PDF file.]:VERSION'
  '(-T --export-text-to-path)'{-T,--export-text-to-path}'[Convert text objects to paths on export, where applicable (for PS, EPS, PDF and SVG export).]'
  --export-latex"[(for PS, EPS, and PDF export) Used for creating images for LaTeX documents, where the image's text is typeset by LaTeX.]"
  --export-ignore-filters'[Export filtered objects (e.g. those with blur) as vectors, ignoring the filters (for PS, EPS, and PDF export).']
  '(-t --export-use-hints)'{-t,--export-use-hints}'[While exporting to PNG, use export filename and DPI hints stored in the exported object (only with --export-id).]'
  '(-b --export-background)'{-b,--export-background=}'[Background color of exported PNG.]:COLOR'
  '(-y --export-background-opacity)'{-y,--export-background-opacity=}'[Opacity of the background of exported PNG.]:VALUE'
  '(-I --query-id)'{-I,--query-id=}'[Set the ID(s) of the object(s) whose dimensions are queried in a comma-separated list.]:OBJECT-ID'
  '(-g --with-gui)'{-g,--with-gui}'[Try to use the GUI (on Unix, use the X server even if $DISPLAY is not set).]'
  '*:file:_files -g "*.svg"'
)
_arguments "${(@)args}"

# vim:ft=zsh
