#compdef dftd4

# 3.7.0

_dftd4() {
  typeset curcontext=$curcontext state lstate line
  typeset -ar args=(
   '(- *)--version[Print program version and exit]'
   '(- *)--citation[Print citation information and exit]'
   '(- *)--license[Print license header and exit]'
   '(- *)'{-h,--help}'[Show help message]'
    '*::dftd4 command:_dftd4_command'
  )
  _arguments "${(@)args}"
}

(( $+functions[_dftd4_command] )) || _dftd4_command() {
  typeset -ar _dftd4_cmds=(
    'run:Evaluate dispersion correction on the provided input structure.'
    'param:Inspect damping parameters.'
  )
  if (( CURRENT == 1 )); then
    _describe -t commands 'dftd4 command' _dftd4_cmds
  else
    typeset curcontext="$curcontext" ret=1

    cmd="${${_dftd4_cmds[(r)$words[1]:*]%%:*}}"

    if (( $#cmd )); then
      curcontext="${curcontext%:*:*}:dftd4-${cmd}:"

      if [ $cmd = 'help' ]; then
        _message 'no more arguments'
      elif ! _call_function ret _dftd4-$cmd; then
        _default && ret=0
      fi
    else
      _default && ret=0
    fi
    return ret
  fi
}

(( $+functions[_dftd4-run] )) || _dftd4-run() {
  typeset -ar methods=(
    'AM05'
    'B-LYP' 'BLYP'
    'B-P' 'BP86' 'BP' 'B-P86'
    'B1B95'
    'B1LYP' 'B1-LYP'
    'B1P' 'B1-P' 'B1P86'
    'B1PW' 'B1-PW' 'B1PW91'
    'B2GPPLYP' 'B2GP-PLYP'
    'B2PLYP' 'B2-PLYP'
    'B3-LYP' 'B3LYP'
    'B3P' 'B3-P' 'B3P86'
    'B3PW' 'B3-PW' 'B3PW91'
    'B97'
    'B97D'
    'B97M'
    'BH-LYP' 'BHLYP'
    'BPBE'
    'BPW' 'B-PW'
    'CAM-B3LYP' 'CAMB3LYP'
    'CAM-QTP01' 'CAMQTP01' 'CAMQTP(01)' 'CAM-QTP(01)'
    'DFTB(MATSCI)'
    'DFTB(MIO)'
    'DFTB(PBC)'
    'DFTB3' 'DFTB(3OB)'
    'DODBLYP' 'DOD-BLYP'
    'DODPBE' 'DOD-PBE'
    'DODPBEB95' 'DOD-PBEB95'
    'DODPBEP86' 'DOD-PBEP86'
    'DODSVWN' 'DOD-SVWN'
    'DSDBLYP' 'DSD-BLYP'
    'DSDPBE' 'DSD-PBE'
    'DSDPBEB95' 'DSD-PBEB95'
    'DSDPBEP86' 'DSD-PBEP86'
    'DSDSVWN' 'DSD-SVWN'
    'GLYP' 'G-LYP'
    'HF'
    'HSE03'
    'HSE06'
    'HSE12'
    'HSE12S'
    'HSESOL'
    'KPr2SCAN50' 'KPr²SCAN50' 'KPr2SCAN50' 'KPr²SCAN50'
    'LB94'
    'LC-BLYP' 'LCBLYP'
    'LC-DFTB' 'DFTB(OB2)'
    'LC-wPBE' 'LCwPBE' 'LC-ωPBE' 'LCωPBE' 'LC-omegaPBE' 'LComegaPBE'
    'LC-wPBEh' 'LCwPBEh' 'LC-ωPBEh' 'LCωPBEh' 'LC-omegaPBEh' 'LComegaPBEh'
    'LH07sSVWN' 'LH07s-SVWN'
    'LH07tSVWN' 'LH07t-SVWN'
    'LH12CTSSIFPW92' 'LH12CT-SSIFPW92'
    'LH12CTSSIRPW92' 'LH12CT-SSIRPW92'
    'LH14TCALPBE' 'LH14T-CALPBE'
    'LH20T'
    'M06'
    'M06L'
    'MN12SX' 'MN12-SX'
    'MPW1B95'
    'MPW1LYP' 'MPW1-LYP'
    'MPW1PW' 'MPW1-PW' 'MPW1PW91'
    'MPW2PLYP'
    'MPWB1K'
    'MPWLYP' 'MPW-LYP'
    'MPWPW' 'MPW-PW' 'MPWPW91'
    'O-LYP' 'OLYP'
    'O3-LYP' 'O3LYP'
    'OPBE'
    'PBE'
    'PBE0'
    'PBE02' 'PBE0-2'
    'PBE0DH' 'PBE0-DH'
    'PBESOL'
    'PR2SCAN50' 'PR²SCAN50' 'PR2SCAN50' 'PR²SCAN50'
    'PR2SCAN69' 'PR²SCAN69' 'PR2SCAN69' 'PR²SCAN69'
    'PW1PW' 'PW1-PW'
    'PW6B95'
    'PW86PBE'
    'PW91'
    'PWP' 'PW-P' 'PW91P86'
    'PWP1'
    'PWPB95'
    'r2SCAN' 'r²SCAN'
    'r2SCAN-3c' 'r²SCAN-3c' 'r2SCAN_3c' 'r²SCAN_3c' 'r2SCAN3c'
    'r2SCAN-CIDH' 'r²SCAN-CIDH' 'r2SCANCIDH' 'r²SCANCIDH'
    'r2SCAN-QIDH' 'r²SCAN-QIDH' 'r2SCANQIDH' 'r²SCANQIDH'
    'r2SCAN0' 'r²SCAN0'
    'r2SCAN0-2' 'r²SCAN0-2' 'r2SCAN02' 'r²SCAN02'
    'r2SCAN0-DH' 'r²SCAN0-DH' 'r2SCAN0DH' 'r²SCAN0DH'
    'r2SCAN50' 'r²SCAN50'
    'r2SCANH' 'r²SCANH'
    'REVDOD-PBEP86' 'REVDODPBEP86'
    'REVDSD-BLYP' 'REVDSDBLYP'
    'REVDSD-PBE' 'REVDSD-PBEPBE' 'REVDSDPBE' 'REVDSDPBEPBE'
    'REVDSD-PBEP86' 'REVDSDPBEP86'
    'REVPBE'
    'REVPBE0'
    'REVPBE0DH' 'REVPBE0-DH'
    'REVPBE38'
    'REVTPSS'
    'REVTPSS0'
    'REVTPSSH'
    'RPBE'
    'RPW86PBE'
    'RSCAN'
    'SCAN'
    'TPSS'
    'TPSS0'
    'TPSSh'
    'wB97' 'ωB97' 'omegaB97'
    'wB97M' 'ωB97M' 'omegaB97M'
    'wB97M-REV' 'ωB97M-REV' 'omegaB97M-REV' 'wB97M_REV' 'ωB97M_REV' 'omegaB97M_REV'
    'wB97X' 'ωB97X' 'omegaB97X'
    'wB97X-3c' 'ωB97X-3c' 'omegaB97X-3c' 'wB97X_3c' 'ωB97X_3c' 'omegaB97X_3c'
    'wB97X-REV' 'ωB97X-REV' 'omegaB97X-REV' 'wB97X_REV' 'ωB97X_REV' 'omegaB97X_REV'
    'wPr2SCAN50' 'wPr²SCAN50' 'wPr2SCAN50' 'wPr²SCAN50'
    'wr2SCAN' 'wr²SCAN'
    'X-LYP' 'XLYP'
    'X3-LYP' 'X3LYP'
  )

  typeset -ar args=(
    '(-c --charge)'{-c,--charge}'[Set charge to molecule, overwrites .CHRG file]:charge'
    '(-f --func)'{-f,--func}'[Use damping parameters for given functional]:method:_values method $methods'
    {-g,--grad}'[Evaluate molecular gradient and virial, write results to file (default: dftd4.txt)]:file:_files'
    --hessian'[Evaluate molecular hessian]'
    --property'[Show dispersion related atomic and system properties]'
    --pair-resolved'[Calculate pairwise representation of dispersion energy]'
    --noedisp'[Disable writing of dispersion energy to .EDISP file]'
    --json'[Dump results to JSON output (default: dftd4.json)]:file:_files -g "*.json"'
    '(-v --verbose)'{-v,--verbose}'[Show more, can be used multiple times]'
    '(-s --silent)'{-s,--silent}'[Show less, use twice to supress all output]'
    '(- *)'{-h,--help}'[Show help message]'
    '*:input:_files -g "(CONTCAR|POSCAR|*.(mol|sdf|vasp|xyz))"'
  )
  _arguments "${(@)args}"
}

(( $+functions[_dftd4-param] )) || _dftd4-param() {
  typeset -ar args=(
    '(- *)'{-h,--help}'[Show help message]'
    '(- *)'{-l,--list,--funcs}'[List all supported functionals]'
  )
  _arguments "${(@)args}"
}

return

: << EOF
Usage: dftd4 [run|param] [options] ...

Generally Applicable Atomic-Charge Dependent London Dispersion Correction.
Takes an geometry input to calculate the D4 dispersion correction.
Periodic calculations are performed automatically for periodic input formats.
Reads .CHRG file (if present) from the same directory as the input.
Specify the functional to select the correct parameters.

Commands

  run       Evaluate dispersion correction on the provided input structure.
            Periodic calculations are performed automatically for periodic inputs
            If no command is specified run is selected by default.

  param     Inspect damping parameters.

Options

-c,--charge <real>       Set charge to molecule, overwrites .CHRG file
-i,--input <format>      Hint for the format of the input file
-f,--func <method>       Use damping parameters for given functional
   --param <list>        Specify parameters for rational damping,
                         expected order is s6, s8, a1, a2 (requires four arguments)
   --mbdscale <s9>       Use scaled ATM three-body dispersion
   --zeta <list>         Adjust charge scaling parameters, takes two reals,
                         expected order is ga, gc (default: 3.0, 2.0)
   --wfactor <real>      Adjust weighting factor for interpolation
                         (default: 6.0)
-g,--grad [file]         Evaluate molecular gradient and virial,
                         write results to file (default: dftd4.txt),
                         attempts to add to Turbomole gradient and gradlatt files
   --hessian             Evaluate molecular hessian
   --property            Show dispersion related atomic and system properties
   --pair-resolved       Calculate pairwise representation of dispersion energy
   --noedisp             Disable writing of dispersion energy to .EDISP file
   --json [file]         Dump results to JSON output (default: dftd4.json)
-v,--verbose             Show more, can be used multiple times
-s,--silent              Show less, use twice to supress all output
   --version             Print program version and exit
   --citation            Print citation information and exit
   --license             Print license header and exit
-h,--help                Show this help message

EOF

# vim:ft=zsh
