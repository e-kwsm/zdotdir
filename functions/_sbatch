#compdef sbatch

_sbatch(){
  _arguments \
    '(-b --begin)'{-b,--begin=}'[Submit the batch script to the Slurm controller immediately, like normal, but tell the controller to defer the allocation of the job until the specified time.]:time' \
    --comment='[An arbitrary comment enclosed in double quotes if using spaces or some special characters.]:string' \
    --deadline='[remove the job if no ending is possible before this deadline]:deadline' \
    '(-d --dependency)'{-d,--dependency=}'[Defer the start of this job until the specified dependencies have been satisfied completed.]:dependency_list' \
    '(-D --chdir)'{-D,--chdir=}'[Set the working directory of the batch script to directory before it is executed]:directory:_path_files -/' \
    '(-e --error)'{-e,--error=}"[Instruct Slurm to connect the batch script's standard error directly to the file name specified in the *filename pattern*.]:filename pattern:_files" \
    --export='[Identify which environment variables from the submission environment are propagated to the launched application.]:spec:_values -s, spec ALL NONE' \
    '(-H --hold)'{-H,--hold}'[Specify the job is to be submitted in a held state (priority of zero).]' \
    '(- *)'{-h,--help}'[Display help information and exit.]' \
    '(-J --job-name)'{-J,--job-name=}'[Specify a name for the job allocation.]:jobname' \
    --mail-type='[Notify user by email when certain event types occur.]:type:_values -s, type NONE BEGIN END FAIL REQUEUE ALL STAGE_OUT TIME_LIMIT TIME_LIMIT_90 TIME_LIMIT_80 TIME_LIMIT_50 ARRAY_TASKS' \
    '(-N --nodes)'{-N,--nodes=}'[Request that a minimum of minnodes nodes be allocated to this job.]:minnodes[-maxnodes]' \
    '(-o --output)'{-o,--output=}"[Instruct Slurm to connect the batch script's standard output directly to the file name specified in the *filename pattern*.]:filename pattern:_files" \
    '(-p --partition)'{-p,--partition=}'[Request a specific partition for the resource allocation.]:partition_names' \
    '(-t --time)'{-t,--time=}'[Set a limit on the total run time of the job allocation.]:time' \
    --time-min='[Set a minimum time limit on the job allocation.]:time' \
    '(- *)'--usage'[Display brief help message and exit.]' \
    '(- *)'{-V,--version}'[Display version information and exit.]' \
    '(-v --verbose)'{-v,--verbose}"[Increase the verbosity of sbatch's informational messages.]" \
    '(-w --nodelist)'{-w,--nodelist=}'[Request a specific list of hosts.]:node name list' \
    '*:script:_files -g "*.sh *.bash"'
}
